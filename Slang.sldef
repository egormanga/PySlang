# Slang syntax definition


## Abstract

codesep: \n | ';'

code: '{' codesep* (expr codesep+)* codesep* '}' | ':' expr (';' expr)* ';'?

block: code


## Primitive

# unary operation
unpreopexpr: unop expr
unpostopexpr: expr unpostop
unopexpr: unpreopexpr | unpostopexpr

# binary operation
binopexpr: expr binop expr

# expression
expr: '(' expr ')' | unopexpr | binopexpr | funccall | itemget | varname | lambda | literal
 # (`f(x+3)` is an instance of `expr`, also `f`, `x+3` and `x` are `expr`s too. It's complicated...)


## Non-final

# left-hand type definition
typename: modifier* type

vardefassignment: identifier ('=' expr)?

# argument definition
argdef: typename? identifier ('[' number ']' | '?' | '+' | '*' | '**' | '=' expr)? | '*' | '/'
 # `, /,` — end of positional-only arguments
 # `, *,` — start of keyword-only arguments
 # in order of positional argument capturing precedence:
 # `arg[n]` — packs a tuple of `n` values from positional arguments
 # `arg?` — optional argument (start capturing only after all required arguments are fulfilled)
 # `arg+` — packs a tuple with at least one value from leftover positionals (incompatible with `arg*`)
 # `arg*` — packs a tuple with any number of values from leftover positionals (incompatible with `arg+`)
 # `arg**` — packs a dict with all leftover keyword arguments
 # `arg=val` — optional argument with a default value

# lambda function
lambda: '(' (argdef (',' argdef)*)? ')' '->' typename (code | '=' expr)

callarg: '*'? expr
callargs: callarg (',' callarg)*

callkwarg: identifier ('=' | ':') expr | '**' expr
callkwargs: callkwarg (',' callkwarg)*

itemget: expr '[' expr ']' # TODO: slices

attrselfop: '@.' | '@' | '.' | ':'
attrop: '->' | attrselfop
attrget: (attrselfop | expr attrop) identifier

classblock: block | classkeyworddef # TODO FIXME
classargdef: typename? varname ('?' | '+' | '*' | '**' | '=' expr)?
classkeyworddefargs: classargdef | '(' classargdef (',' classargdef)* ')'
classkeyworddef: classdefkeyword classkeyworddefargs block

## Final

# function definition
@funcdef: (typename | 'def') identifier '(' argdef (',' argdef)* ')' (code | '=' expr)

# keyword expression
@keywordexpr: exprkeyword expr?

# variable definition
@vardef: typename vardefassignment (',' vardefassignment)*

@assignment: varname '=' expr

@inplaceopassignment: varname [binop '='] expr

@unpackassignment: varname (',' varname)* '=' expr

@inplaceopunpackassignment: varname (',' varname)* [binop '='] expr

# function call
@funccall: expr '(' (callargs | callkwargs | callargs ',' callkwargs) ')'

@conditional: 'if' expr block ('elif' expr block)*

@forloop: 'for' identifier 'in' expr block

@whileloop: 'while' expr block

@elseclause: 'else' block

# keyword block definition
@keyworddef: defkeyword block

# class definition
@classdef: 'class' identifier ('(' identifier (',' identifier)* ')')? '{' classblock '}'


## Keywords

# return from function
return: 'return' expr

# import module
import: 'import' (identifier ':')? (identifier '/')* identifier (':' (identifier | '*'))?  # ns:path/to/package:names

# delete variable
delete: 'delete' varname

assert: 'assert' expr

exprkeyword: return | 'break' | 'continue' | import | delete | assert | 'super' | 'breakpoint'

defkeyword: 'main' | 'exit'

classdefkeyword: 'init' | 'constr' | 'property' | 'repr' | 'eq'


## Modifiers

modifier: 'const' | 'static' | 'volatile'
 # `const` — immutable/constant variable


## Reserved keywords

reserved: 'try' | 'catch' | 'except' | 'finally' | 'raise' | 'with' | 'yield' | 'include' | 'using' | 'default'


## Identifiers

identifier: /[^\W\d][\w]*/
 # non-empty sequence of alphanumeric characters plus underscore («_»), not starting with a digit character.

varname: attrget | identifier


## Data types

type: ['u'? ('int' | 'float' | 'complex')] |
      [('i' | 'u' | 'u'? ('f' | 'c')) ('8' | '16' | '32' | '64' | '128')] |
      'bool' | 'byte' | 'char' | 'str' | 'void' | 'auto'
 # `i8`, `i16`, `i32`, `i64`, `i128` — fixed size integer
 # `u8`, `u16`, `u32`, `u64`, `u128` — fixed size unsigned integer
 # `f8`, `f16`, `f32`, `f64`, `f128` — fixed size IEEE-754 floating point number
 # `uf8`, `uf16`, `uf32`, `uf64`, `uf128` — fixed size unsigned floating point number
 # `c8`, `c16`, `c32`, `c64`, `c128` — fixed size complex number
 # `uc8`, `uc16`, `uc32`, `uc64`, `uc128` — fixed size unsigned complex number
 # `int` — unsized («big») integer
 # `uint` — unsized unsigned integer
 # `float` — unsized floating point number
 # `ufloat` — unsized unsigned floating point
 # `complex` — unsized complex number
 # `ucomplex` — unsized unsigned complex number
 # `bool` — logical (boolean) value
 # `byte` — single byte
 # `char` — UTF-8 character
 # `str` — UTF-8 string
 # `void` — nothing (or anything...)
 # `auto` — compile-time type deduction based on value


## Literals

number: /[^\D0]\d*/

char: /\\ ([\\'"abefnrtv] | N\{[0-9A-Z\ -]+\} | u[0-9A-Fa-f]{4} | U[0-9A-Fa-f]{8} | x[0-9A-Fa-f]{1,2} | [0-7]{1,3}) | [^\\]/

character: [\' char \']

string: ['f'? ('"' char* '"' | \' char* \')]

list: '[' type (':' expr (',' expr)*)? ']'

tuple: '(' type expr (',' type expr)* ')'

map: '{' expr ':' expr (',' expr ':' expr)* '}'

set: '{' expr (',' expr)* '}'

literal: number | character | string | list | tuple | map | set


## Operators

unchop: '!' | '+' | '-' | '~'
undchop: '++' | '--'
unkwop: 'not'
unmathop: 'floor' | 'ceil' | 'round' |
          ['i'? 'sqrt'] |
          ['exp' 'm1'?] |
          ('ln' | 'lb' | 'lg' | ['log' ('2' | '10' | '1p')]) |
          ['a'? (('sin' | 'cos' | ('tan' | 'tg') | ('cotan' | 'ctg')) 'h'? | 'tan2')]
unop: undchop | unchop | unkwop | unmathop

unchpostop: '!'
undchpostop: '++' | '--' | '**'
unpostop: undchpostop | unchpostop

binchop: '%' | '&' | '*' | '+' | '-' | '/' | '<' | '=' | '>' | '^' | '|'
bindchop: '==' | '**' | '//' | '<<' | '>>'
binkwop: 'is' | 'is not' | 'in' | 'not in' | 'not' | 'and' | 'but' | 'xor' | 'or' | 'isof'
binmathop: 'pow'
binop: bindchop | binchop | binkwop | binmathop


## Specials

:whitespace: ' ' | \t | \r | \v | \f

:comment: \#

@blockcomment: [\# '|'] /(.+?)\s*\|\#/ ['|' \#]

:continuation: \\

:special: '..' | ':=' | ',' | '?' | '=' | '(' | ')' | '{' | '}'

# expression evaluation (only in REPL)
:replexpr: expr


# by Sdore, 2021-22
#  slang.sdore.me
